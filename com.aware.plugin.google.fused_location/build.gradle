apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "27.0.1"

    defaultConfig {
        applicationId "com.aware.plugin.google.fused_location"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode version_code
        versionName version_readable
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        abortOnError false
    }
}

if (System.getenv("storeFile") != null && System.getenv("storePassword") != null && System.getenv("keyAlias") != null && System.getenv("keyPassword") != null) {
    android.signingConfigs.release.storeFile = file(System.getenv("storeFile"))
    android.signingConfigs.release.storePassword = System.getenv("storePassword")
    android.signingConfigs.release.keyAlias = System.getenv("keyAlias")
    android.signingConfigs.release.keyPassword = System.getenv("keyPassword")
} else {
    println("No keystore defined. Unsigned!")
    android.buildTypes.release.signingConfig = null
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    maven { url "https://jitpack.io" } //AWARE development
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.android.support:appcompat-v7:$support_libs"
    compile "com.android.support:gridlayout-v7:$support_libs"
    compile "com.github.denzilferreira:aware-client:$aware_libs"
    compile "com.google.android.gms:play-services-location:$google_libs"
    compile "com.android.support:design:$support_libs"
    compile "com.google.android.gms:play-services-maps:$google_libs"
}